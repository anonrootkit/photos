plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'com.google.firebase.firebase-perf'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs.kotlin'
    id 'com.google.protobuf'
}

android {
    compileSdk 32

    defaultConfig {
        applicationId "com.fiore.photos"
        minSdk 21
        targetSdk 32
        versionCode rootProject.version_code
        versionName rootProject.version_name

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "PREFERENCE_NAME", PREFERENCE_NAME
        buildConfigField "String", "USER_PROFILE_PREFERENCE", USER_PROFILE_PREFERENCE
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures.dataBinding = true
}

dependencies {
    // Firebase
    implementation platform('com.google.firebase:firebase-bom:' + firebase_bom_version)
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-storage-ktx'
    implementation 'com.google.firebase:firebase-firestore-ktx'
    implementation 'com.google.firebase:firebase-perf-ktx'
    implementation 'com.google.firebase:firebase-auth-ktx'

    // Android Core
    implementation 'androidx.core:core-ktx:' + core_ktx_version
    implementation 'androidx.appcompat:appcompat:' + appcompat_version
    implementation 'com.google.android.material:material:' + material_version
    implementation "androidx.fragment:fragment-ktx:" + fragment_version
    implementation 'androidx.navigation:navigation-fragment-ktx:' + navigation_version
    implementation 'androidx.navigation:navigation-ui-ktx:' + navigation_version
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:' + lifecycle_version

    // Hilt
    implementation 'com.google.dagger:hilt-android:' + hilt_version
    kapt 'com.google.dagger:hilt-compiler:' + hilt_version

    // Datastore
    implementation 'androidx.datastore:datastore-preferences:' + datastore_version
    implementation  'androidx.datastore:datastore-core:' + datastore_version
    implementation  'com.google.protobuf:protobuf-javalite:' + protobuf_version

    // Splash
    implementation 'androidx.core:core-splashscreen:' + splash_screen_version
}

protobuf {
    protoc {
//        artifact = "com.google.protobuf:protoc:3.14.0"
        artifact = "com.google.protobuf:protoc:3.14.0:osx-x86_64"
    }

    // Generates the java Protobuf-lite code for the Protobufs in this project. See
    // https://github.com/google/protobuf-gradle-plugin#customizing-protobuf-compilation
    // for more information.
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}